\DOC cv_trans_rec

\TYPE {cv_trans_rec : thm -> tactic -> unit}

\SYNOPSIS
Translates functional definitions to the {cv_compute} subset of HOL.

\LIBRARY
cv_transLib

\DESCRIBE
This function is that the same as {cv_transLib.cv_trans} except that
{cv_transLib.cv_trans_rec} takes a user-provided tactic for proving the
termination goal for the translator-defined {:cv} function.

\FAILURE
When the translation produced a precondition that {cv_transLib.cv_trans} is
unable to automatically prove, or encounters a sub-term containing a constant
that has not already been translated, or the provided tactic is insufficient
for proving the termination goal arising from the translator-defined {:cv}
function.

\EXAMPLE

{
> Definition count_up_def:
  count_up m k = if m < k:num then 1 + count_up (m+1) k else 0:num
Termination
  WF_REL_TAC ‘measure $ λ(m,k). k - m:num’
End;
Equations stored under "count_up_def".
Induction stored under "count_up_ind".
val count_up_def =
   ⊢ ∀m k. count_up m k = if m < k then 1 + count_up (m + 1) k else 0: thm
> cv_trans_rec count_up_def
  (WF_REL_TAC ‘measure $ λ(m,k). cv$c2n k - cv$c2n m’
   \\ Cases \\ Cases \\ gvs [] \\ rw [] \\ gvs []);
Equations stored under "cv_count_up_def".
Induction stored under "cv_count_up_ind".
Finished translating count_up, stored in cv_count_up_thm
val it = ⊢ (): unit
> cv_eval “count_up 5 100”;
val it = ⊢ count_up 5 100 = 95: thm
}

\COMMENTS
Designed to produce definitions suitable for evaluation by {cv_transLib.cv_eval}.

\SEEALSO
cv_transLib.cv_trans,
cv_transLib.cv_trans_pre,
cv_transLib.cv_trans_pre_rec,
cv_transLib.cv_trans_rec,
cv_transLib.cv_auto_trans,
cv_transLib.cv_auto_trans_pre,
cv_transLib.cv_auto_trans_pre_rec,
cv_transLib.cv_auto_trans_rec,
cv_transLib.cv_eval,
cv_transLib.cv_termination_tac

\ENDDOC
